plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystoreProperties = new Properties()

def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        config {
            storeFile keystoreProperties["storeFile"] ? file(keystoreProperties["storeFile"]) : null
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    compileSdk 32

    defaultConfig {
        applicationId "net.mfuertes.aac"
        minSdk 29
        //noinspection ExpiredTargetSdkVersion
        targetSdk 31
        versionCode findProperty("versionCode") ? (findProperty("versionCode") as int) : 1
        versionName findProperty("versionName") ?: "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            signingConfig findProperty("signDebug") ? signingConfigs.config : signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig findProperty("signRelease") ? signingConfigs.config : signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation 'com.linkedin.dexmaker:dexmaker:2.28.1'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    implementation 'com.google.protobuf:protobuf-javalite:3.8.0'
}

